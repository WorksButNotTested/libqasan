import "../just/env.just"
import "../dummy_libc/Justfile"

QASAN_SOURCE_DIR := source_directory()

[unix]
compile_qasan:
	#!/bin/bash
	source {{ DOTENV }}
	cargo \
		+${CHANNEL} \
		build \
		--package qasan \
		--target ${TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }}

[unix]
fix_qasan:
	#!/bin/bash
	source {{ DOTENV }}
	cargo \
		+${CHANNEL} \
		fix \
		--package qasan \
		--target ${TARGET} \
		--profile {{ PROFILE }} \
		--target-dir {{ TARGET_DIR }} \
		--features test \
		--allow-dirty

[unix]
link_qasan: compile_qasan build_dummy
	#!/bin/bash
	source {{ DOTENV }}
	${ASAN_CC} \
		${ASAN_CFLAGS} \
		-shared \
		-nodefaultlibs \
		-nostartfiles \
		-nostdlib \
		-g \
		-u aligned_alloc \
		-u atoi \
		-u atol \
		-u atoll \
		-u calloc \
		-u free \
		-u malloc \
		-u malloc_usable_size \
		-u memalign \
		-u memset \
		-u mmap \
		-u munmap \
		-u posix_memalign \
		-u pvalloc \
		-u read \
		-u realloc \
		-u reallocarray \
		-u valloc \
		-u write \
		-o {{ TARGET_DIR }}/$TARGET/{{ PROFILE_DIR }}/_libqasan.so \
		-Wl,--version-script={{ QASAN_SOURCE_DIR }}/qasan.map \
		-Wl,--gc-sections \
		-Wl,--no-undefined \
		{{ TARGET_DIR }}/$TARGET/{{ PROFILE_DIR }}/libqasan.a \
		-L {{ TARGET_DIR }}/$TARGET/{{ PROFILE_DIR }} \
		"-ldummy_libc"

[unix]
strip_qasan: link_qasan
	#!/bin/bash
	source {{ DOTENV }}
	${ASAN_STRIP} \
		--strip-unneeded \
		-o {{ TARGET_DIR }}/$TARGET/{{ PROFILE_DIR }}/libqasan.so \
		{{ TARGET_DIR }}/$TARGET/{{ PROFILE_DIR }}/_libqasan.so

[unix]
build_qasan: strip_qasan
